#!/usr/local/bin/perl -w
# -*- CPerl -*-
# $Id$

use strict;
use IO::File;
use File::Find;
use File::Basename;
use Image::Size qw(html_imgsize);

# index.htmlで、最近 N 日分を表示するか
my $RECENT = 5;

# HTMLファイルの拡張子
my $HTML_SUFFIX = '.html';

# サムネールを置くディレクトリ
my $THUMBS_DIR = "thumbs";

# HTMLの雛型のあるディレクトリ
my $TEMPLATE_DIR = '/home/masao/CVSwork/p-album/templates';

# convert コマンドのオプション
my @CONVERT_OPT = ('-geometry', '15%');

main();
sub main {
    my @files = find_photos();
    my $i = 0;
    foreach my $fname (@files) {
	print "$fname\n";
	make_thumbnail($fname) if not -e thumbname($fname);
	make_htmlpage($fname, $files[$i-1], $files[$i+1]);
	$i++;
    }
    make_monthlypage(@files);
}

sub usage() {
    print "  Usage: $0 dir\n";
    exit;
}

sub find_photos() {
    opendir(DIR, ".") || die "opendir: .: $!";
    my @files = sort grep(/^\d{4}-\d{2}-\d{2}T\d{2}:\d{2}:\d{2}\.jpg$/,
		       readdir(DIR));
    closedir(DIR) || die "closedir: $!";
    return @files;
}

sub make_thumbnail($) {
    my ($fname) = @_;
    my $thumb = thumbname($fname);
    my @cmd = ("convert", @CONVERT_OPT, $fname, $thumb);
    system(@cmd) == 0
	|| die "system fail: \"@cmd\": $!";
}

sub make_htmlpage($$$) {
    my ($fname, $prevfile, $nextfile) = @_;

    my %param = ();
    my ($year, $month, $day, $hour, $min, $sec) = split(/\D+/, $fname);
    $param{'date'} = "$year-$month-$day";
    $param{'time'} = "$hour:$min:$sec";
    $param{'monthlyindex'} = "$year-$month$HTML_SUFFIX";
    $param{'image'} = basename($fname);
    $param{'imagesize'} = html_imgsize($fname);
    $param{'prev'} = htmlname($prevfile) if -e $prevfile;
    $param{'next'} = htmlname($nextfile) if -e $nextfile;

    my $template = readfile("$TEMPLATE_DIR/htmlpage.html");
    $template =~ s/\$(\w+)/defined($param{$1})? $param{$1} : ""/ge;

    my $html = htmlname($fname);
    my $fh = fopen(">$html");
    print $fh $template;
}

sub make_monthlypage(@) {
    my (@files) = @_;

    my %daybody = ();
    my %monthbody = ();
    foreach my $fname (@files) {
	my ($year, $month, $day, $time) = get_datetime($fname);
	my $html = htmlname($fname);
	my $thumb = thumbname($fname);
	my $size = html_imgsize($thumb);
	$daybody{"$year-$month-$day"} .= "<a href=\"$html\"><img src=\"$thumb\" $size></a>\n";
	$monthbody{"$year-$month"} = 1;
    }
    my @month = sort keys %monthbody;
    for (my $i = 0; $i < @month; $i++) {
	my @days = sort grep /^$month[$i]/, keys %daybody;
	my %param = ();
	$param{'body'} = "<div class=\"monthbody\">\n";
	foreach my $day (@days) {
	    $param{'body'} .= "<div class=\"day-header\"><a name=\"$day\">$day</a></div>\n";
	    $param{'body'} .= "<div class=\"day-body\">$daybody{$day}</div>\n";
	}
	$param{'body'} .= "</div>";
	$param{'month'} = $month[$i];
	if ($i > 0) {
	    $param{'prev'} = "<a href=\"$month[$i-1].html\">前月</a>";
	} else {
	    $param{'prev'} = "前月";
	}
	if ($i < $#month) {
	    $param{'next'} = "<a href=\"$month[$i+1].html\">翌月</a>";
	} else {
	    $param{'next'} = "翌月";
	}

	my $template = readfile("$TEMPLATE_DIR/monthlypage.html");
	$template =~ s/\$(\w+)/defined($param{$1})? $param{$1} : ""/ge;
	my $fh = fopen(">$month[$i].html");
	print $fh $template;
    }

    # index.html には最新のものを書き出す。
    my %param = ();
    $param{'recent'} = $RECENT;
    $param{'body'} = "<div class=\"body\">\n";
    my @days = sort { $b cmp $a } keys %daybody;
    for (my $i = 0; $i < @days && $i < $RECENT; $i++) {
	$param{'body'} .= "<div class=\"day-header\">$days[$i]</div>\n";
	$param{'body'} .= "<div class=\"day-body\">$daybody{$days[$i]}</div>\n";
	$param{'body'} .= "</div>";
    }
    my $prev_year = 0;
    for my $i (sort keys %monthbody) {
        my ($year, $month) = ($i =~ /^(\d{4})-(\d{2})/);
        if ($year ne $prev_year) { # year changed!
	    $param{'monthly_list'} .= "<br>\n" if $prev_year;
	    $param{'monthly_list'} .= "$year : \n";
            $prev_year = $year;
        }
        $param{'monthly_list'} .= "<a href=\"$i.html\">$month</a>\n";
    }

    my $template = readfile("$TEMPLATE_DIR/indexpage.html");
    $template =~ s/\$(\w+)/defined($param{$1})? $param{$1} : ""/ge;
    my $fh = fopen(">index.html");
    print $fh $template;
}

sub get_monthlypage_list() {
}

sub get_datetime($) {
    my ($fname) = @_;
    return split(/\D+/, $fname);
}

sub thumbname($) {
    my ($fname) = @_;
    return "$THUMBS_DIR/$fname";
}

sub htmlname($) {
    my ($fname) = @_;
    my ($base, $dir, $ext) = fileparse($fname, '\.jpg');
    return "$dir$base$HTML_SUFFIX";
}

sub fopen($) {
    my ($fname) = @_;
    my $fh = new IO::File;
    $fh->open($fname) || die "fopen: $fname: $!";
    return $fh;
}

sub readfile ($) {
    my ($fname) = @_;
    my $fh = fopen($fname);
    my $cont = '';
    my $size = -s $fh;
    read $fh, $cont, $size;
    $fh->close;
    return $cont;
}
